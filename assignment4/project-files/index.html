<!doctype html>
<html lang='en'>
  <head>
    <style>body{ margin:0; background: black; }</style>
    <script src='https://cdn.jsdelivr.net/npm/tweakpane@3.0.7/dist/tweakpane.min.js'></script>
  </head>
  <body>
    <canvas id='gl'></canvas>
  </body>

  <script id='copyVertex' type='x-shader/x-vertex'>#version 300 es
    in vec2 a_pos;

    void main() {
      gl_Position = vec4( a_pos, 0, 1 );
    }
  </script>

  <!-- render to screen shader -->
  <script id='copyFragment' type='x-shader/x-fragment'>#version 300 es
    #ifdef GL_ES
    precision mediump float;
    #endif

    uniform sampler2D uSampler;
    uniform vec2 resolution;

    out vec4 color;
    void main() {
      vec2 pos = gl_FragCoord.xy / resolution;
      vec4 tex = texture( uSampler, pos );

      vec4 slimeMask = vec4(tex.r, tex.r, tex.r, 1.);
      vec4 factorColoring = vec4(1. - tex.g, 1. - tex.g, 1., 1.);

      color = slimeMask * factorColoring;
    }
  </script>

  <script id='simulationVertex' type='x-shader/x-vertex'>#version 300 es
    #define PI_4 3.1415926538/4.
    precision mediump float;
    
    // input from our feedback TRANSFORM_FEEDBACK
    in vec4 a_pos;
    
    uniform vec2 resolution;
    
    // our chemical layer
    uniform sampler2D uSampler;

    uniform vec2 uMousePos;
    uniform float uSensorDistance;
    uniform float uSensorAngle;
    uniform vec3 uSensorDirectionWeights;
    uniform float uAgentSpeed;
    uniform float uMousePolarity;
    
    // the output of our feedback transform
    // xy will store our position
    // zw wiil store our heading / direction
    out vec4 o_vpos;

    // output for tracking state
    out vec4 o_state;
    
    // this function accepts a direction (header) for a
    // agent and a rotation in radians, returning the
    // new, rotated direction
    vec2 rotate(vec2 dir, float angle) {
      float  s = sin( angle );
      float  c = cos( angle );
      mat2   m = mat2( c, -s, s, c );
      return m * dir;
    }
    
    // pos - position of agent
    // dir - heading of agent
    // angle - direction to sense, in radians
    // distance - distance to sense
    float readSensor( vec2 pos, vec2 dir, float angle, vec2 distance ) {

      vec2 nMousePos = uMousePos / resolution;
      vec2 newangle  = rotate( dir, angle );
      vec2 offset = newangle * distance;

      if ( uMousePolarity != 0. ) {

        // Compare direction to mouse with sensor direction
        // Set artificially high/low value if in range + sensing toward mouse
        
        float mouseDot = dot(normalize(nMousePos - pos), normalize(newangle));
        float mouseFieldDist = length(nMousePos - pos);

        if ( mouseFieldDist < length(distance) ) {
          float factor = mouseDot * (1. - mouseFieldDist / length(distance));
          o_state.x = max(o_state.x, abs(factor));
          return factor * 1000. * uMousePolarity;
        }
      }

      return texture( uSampler, pos + offset ).r;
    } 
    
    void main() {
      // get normalized height / width of a single pixel 
      vec2 pixel = 1. / resolution;
    
      // how far ahead should sensing occur? this is fun to play with
      vec2 sensorDistance = pixel * uSensorDistance;
    
      // normalize our {-1,1} vertex coordinates to {0,1} for texture lookups
      vec2 pos = (1. + a_pos.xy) / 2.;
    

      o_state = vec4(0.);

      float sensorHalfAngle = uSensorAngle / 2.;

      // read sensor information at different angles
      float left    = readSensor( pos, a_pos.zw, -sensorHalfAngle, sensorDistance ) * uSensorDirectionWeights.x;
      float forward = readSensor( pos, a_pos.zw, 0.,               sensorDistance ) * uSensorDirectionWeights.y;
      float right   = readSensor( pos, a_pos.zw, sensorHalfAngle,  sensorDistance ) * uSensorDirectionWeights.z;
      
      // initialize feedback transform output
      o_vpos = a_pos;

      // change direction if left or right sensor is higher than forward
      if( left > forward && left > right ) {

        // rotate left and store in .zw
        o_vpos.zw = rotate( o_vpos.zw, -sensorHalfAngle );
      
      } else if( right > left && right > forward ) {  
        
        o_vpos.zw = rotate( o_vpos.zw, sensorHalfAngle );
      
      } else if ( right == left ) {
        
        // randomly pick a direction
        float rand = fract(sin(a_pos.x)*100000.0);
        float angleSign = (rand > 0.5) ? 1. : -1.;
        o_vpos.zw = rotate( o_vpos.zw, angleSign * sensorHalfAngle );
      }
      
      // move our agent in our new direction by one pixel
      o_vpos.xy += o_vpos.zw * pixel * uAgentSpeed;
      
      gl_PointSize = 1.;
    
      // position is for fragment shader rendering, don't need to include heading
      gl_Position = vec4( a_pos.x, a_pos.y, 0., 1. );
    }
  </script>

  <script id='simulationFragment' type='x-shader/x-fragment'>#version 300 es
    precision mediump float;
    
    uniform vec2 resolution;

    in  vec4 o_vpos;
    in  vec4 o_state;
    out vec4 o_frag;
    
    void main() {
      float f = normalize(o_vpos.zw).y;
      o_frag = vec4( 1., o_state.x, 0., 0.1 );
    } 
  </script>

  <script id='ddFragment' type='x-shader/x-fragment'>#version 300 es
    #ifdef GL_ES
    precision mediump float;
    #endif  
    
    uniform sampler2D uSampler;
    uniform vec2 resolution;
    
    float get(int x, int y) {
      return texture( uSampler, ( gl_FragCoord.xy + vec2(x, y) ) / resolution ).r;
    }
    
    out vec4 color;
    void main() {
      float sum = get(0,0) - 1.;
      
      sum += get( 1,  0) * .2;
      sum += get(-1,  0) * .2;
      sum += get( 0,  1) * .2;
      sum += get( 0, -1) * .2;

      sum += get( 1,  1) * .05;
      sum += get( 1, -1) * .05;       
      sum += get(-1,  1) * .05;
      sum += get(-1, -1) * .05;
    
      vec2 pos = gl_FragCoord.xy / resolution;
      color = vec4( sum * .9, 0., 0., .25 );
    }
    </script>

  <script src='main.js'></script>

</html>
